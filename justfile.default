# This allows the downstream justfile importing this file to override values and recepies.

set allow-duplicate-variables := true
set allow-duplicate-recipes := true

# [no-cd] is required because when this justfile is used with `import dev/justfile.default` the invocation folder will be `dev` otherwise

[no-cd]
_build target *args:
    nix build "{{ justfile_directory() + '#' + target }}" {{ if args != "" { '-- ' + args } else { '' } }}

[no-cd]
_develop target:
    nix develop "{{ justfile_directory() + '#' + target }}" 

[no-cd]
_run target *args:
    nix run "{{ justfile_directory() + '#' + target }}" {{ if args != "" { '-- ' + args } else { '' } }}

[no-cd]
check:
    nix flake check "{{ justfile_directory() }}"

[no-cd]
fmt:
    nix fmt {{ justfile_directory() }}

[no-cd]
generate-pre-commit:
    nix develop "{{ justfile_directory() + '#pre-commit' }}"
