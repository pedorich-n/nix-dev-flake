# This allows the downstream justfile importing this file to override values and recepies.

set allow-duplicate-variables := true
set allow-duplicate-recipes := true

extra_dev_config := ""
input_override := if extra_dev_config != "" { "--override-input extra-config path:" + extra_dev_config } else { "" }

# The `?dir=dev` part is important. It executes nix command from  the parent folder, pointing to a sub-folder instead of entering the subfolder.
# If you enter a folder of a submodule and execute nix command from there, it will copy the submodule to its own /nix/store folder, without parent.

# [no-cd] is required because when this justfile is used with `import dev/justfile.default` the invocation folder will be `dev` otherwise

[no-cd]
check-fmt:
    nix flake check "{{ justfile_directory() + '?dir=dev&submodules=1' }}" {{ input_override }}

# No need to use `?dir=dev` here, because `fmt` isn't a part of flake command, yet it uses output.<system>.formatter package.
# ðŸ˜• Very unintuitive

[no-cd]
fmt:
    cd {{ justfile_directory() / "dev" }} && nix fmt {{ justfile_directory() }} {{ input_override }}

[no-cd]
generate-pre-commit:
    nix develop "{{ justfile_directory() + '?dir=dev&submodules=1#pre-commit' }}" {{ input_override }}
